***Settings***
Library    SeleniumLibrary
Library    ./libraries/utils_library.py
Variables    ./credentials.py

***Variables***
${BROWSER}         Chrome
${SITE_URL}        http://localhost:4100/
${LOGIN_URL}       http://localhost:4100/login

${LOGIN_USERNAME_FIELD}    xpath=//input[@placeholder='Email']
${LOGIN_PASSWORD_FIELD}    xpath=//input[@placeholder='Password']
${LOGIN_BUTTON}            xpath=//button[@type='submit' and text()='Sign in']

${WRITE_POST_BUTTON}     xpath=//a[@href='/editor' and contains(., 'New Post')]
${POST_TITLE_FIELD}      xpath=//input[@placeholder='Article Title']
${POST_TOPIC_FIELD}      xpath=//input[@placeholder=\"What's this article about?\"]
${POST_CONTENT_FIELD}    xpath=//textarea[@placeholder='Write your article (in markdown)']
${SUBMIT_POST_BUTTON}    xpath=//button[contains(@class, 'btn-primary') and text()='Publish Article']

${PROFILE_LINK_NAVBAR}    css=a.nav-link[href^='/@']
${EDIT_ARTICLE_BUTTON}    xpath=//a[contains(@class, 'btn-outline-secondary') and contains(@class, 'btn-sm') and normalize-space(.)='Edit Article']
${DELETE_ARTICLE_BUTTON}  xpath=//button[contains(@class, 'btn-outline-danger') and contains(@class, 'btn-sm') and contains(normalize-space(.), 'Delete Article')]

${GLOBAL_FEED_LINK}       xpath=//a[contains(@class, 'nav-link') and text()='Global Feed']

***Test Cases***
게시글 작성
    [Documentation]    사이트에 접속하여 로그인 후 새 글을 작성하고 완료를 확인합니다.
    웹사이트 접속 및 로그인
    글 작성 페이지로 이동
    글 내용 작성 및 제출
    글 작성 완료 확인
    [Teardown]    Close Browser

내용 없이 글 작성 시도
    [Documentation]    내용을 입력하지 않고 글 작성을 시도하고 오류를 확인합니다.
    웹사이트 접속 및 로그인
    글 작성 페이지로 이동
    제목 없이 글 내용 작성 및 제출 시도
    주제 없이 글 내용 작성 및 제출 시도
    내용 없이 글 내용 작성 및 제출 시도
    [Teardown]    Close Browser

내 프로필에서 작성 글 확인
    [Documentation]    작성한 글을 내 프로필에서 확인할 수 있는지 확인합니다.
    웹사이트 접속 및 로그인
    글 작성 페이지로 이동
    글 내용 작성 및 제출
    상단 닉네임을 클릭해 내 프로필로 이동
    작성한 게시글이 목록에 있는지 확인
    [Teardown]    Close Browser

게시글 수정
    [Documentation]    게시글을 정상적으로 수정할 수 있는지 확인합니다.
    웹사이트 접속 및 로그인
    글 작성 페이지로 이동
    글 내용 작성 및 제출
    글 수정 페이지로 이동
    제목, 주제, 본문 내용 수정
    글 상세 페이지에 수정된 제목, 본문 내용이 반영되었는지 확인
    [Teardown]    Close Browser

게시글 삭제
    [Documentation]    게시글을 정상적으로 삭제할 수 있는지 확인합니다.
    웹사이트 접속 및 로그인
    글 작성 페이지로 이동
    글 내용 작성 및 제출
    글 삭제
    상단 닉네임을 클릭해 내 프로필로 이동
    삭제된 게시글이 더 이상 표시되지 않는지 확인
    [Teardown]    Close Browser

타인의 게시글 수정/삭제 제한
    [Documentation]    타인의 게시글을 수정/삭제할 수 없는지 확인합니다.
    웹사이트 접속 및 로그인
    글로벌 피드로 이동
    다른 사용자의 게시글 찾아서 클릭
    게시글 상세 화면에서 수정/삭제 버튼이 없는지 확인
    [Teardown]    Close Browser

***Keywords***
웹사이트 접속 및 로그인
    Open Browser    ${LOGIN_URL}    ${BROWSER}
    Maximize Browser Window
    Wait Until Page Contains Element    ${LOGIN_USERNAME_FIELD}    timeout=10s
    Input Text    ${LOGIN_USERNAME_FIELD}    ${USERNAME}
    Input Password    ${LOGIN_PASSWORD_FIELD}    ${PASSWORD}
    Click Button    ${LOGIN_BUTTON}
    Wait Until Page Contains Element    ${WRITE_POST_BUTTON}    timeout=15s
    Log    로그인 성공: 'New Post' 버튼 확인됨

글 작성 페이지로 이동
    Wait Until Page Contains Element    ${WRITE_POST_BUTTON}    timeout=15s
    Click Element    ${WRITE_POST_BUTTON}
    ${current_url}=    Get Location
    Log    Clicked 'New Post'. Current URL is: ${current_url}
    Should Contain    ${current_url}    /editor    msg=Should navigate to the editor page.
    Wait Until Page Contains Element    ${POST_TITLE_FIELD}    timeout=20s
    Log    글 작성 페이지로 이동 완료 (Editor page loaded with title field)

글 내용 작성 및 제출
    Wait Until Page Contains Element    ${POST_TITLE_FIELD}    timeout=20s
    ${unique_title}=    Generate Unique Text    Robot Test Title
    ${unique_topic}=    Generate Unique Text    Robot Test Topic
    ${unique_content}=  Generate Unique Text    This is a unique article content generated by Robot Framework.

    Input Text    ${POST_TITLE_FIELD}    ${unique_title}
    Input Text    ${POST_TOPIC_FIELD}    ${unique_topic}
    Input Text    ${POST_CONTENT_FIELD}    ${unique_content}
    Click Button    ${SUBMIT_POST_BUTTON}
    Log    글 내용 작성 및 제출 완료
    Set Test Variable    ${GENERATED_POST_TITLE}    ${unique_title}

글 작성 완료 확인
    Wait Until Page Contains    ${GENERATED_POST_TITLE}    timeout=20s
    Page Should Contain    ${GENERATED_POST_TITLE}
    Log    글 작성 완료 확인: "${GENERATED_POST_TITLE}" 제목의 글이 페이지에 표시됨

제목 없이 글 내용 작성 및 제출 시도
    Wait Until Page Contains Element    ${POST_TITLE_FIELD}    timeout=10s
    # 제목은 비워둡니다.
    ${unique_topic}=    Generate Unique Text    Robot Test Topic No Title
    ${unique_content}=  Generate Unique Text    This is content for a post without a title.

    Input Text    ${POST_TOPIC_FIELD}    ${unique_topic}
    Input Text    ${POST_CONTENT_FIELD}    ${unique_content}
    Click Button    ${SUBMIT_POST_BUTTON}
    Log    제목 없이 글 내용 작성 및 제출 시도 완료
    ${current_url}=    Get Location
    Should Be Equal As Strings    ${current_url}    http://localhost:4100/editor
    Go To    http://localhost:4100/editor
    Wait Until Page Contains Element    ${POST_TITLE_FIELD}    timeout=10s

주제 없이 글 내용 작성 및 제출 시도
    Wait Until Page Contains Element    ${POST_TITLE_FIELD}    timeout=10s
    ${unique_title}=    Generate Unique Text    Robot Test Title No Topic
    # 주제는 비워둡니다.
    ${unique_content}=  Generate Unique Text    This is content for a post without a topic.

    Input Text    ${POST_TITLE_FIELD}    ${unique_title}
    Input Text    ${POST_CONTENT_FIELD}    ${unique_content}
    Click Button    ${SUBMIT_POST_BUTTON}
    Log    주제 없이 글 내용 작성 및 제출 시도 완료
    ${current_url}=    Get Location
    Should Be Equal As Strings    ${current_url}    http://localhost:4100/editor
    Go To    http://localhost:4100/editor
    Wait Until Page Contains Element    ${POST_TITLE_FIELD}    timeout=10s 

내용 없이 글 내용 작성 및 제출 시도
    Wait Until Page Contains Element    ${POST_TITLE_FIELD}    timeout=10s
    ${unique_title}=    Generate Unique Text    Robot Test Title No Content
    ${unique_topic}=    Generate Unique Text    Robot Test Topic No Content

    Input Text    ${POST_TITLE_FIELD}    ${unique_title}
    Input Text    ${POST_TOPIC_FIELD}    ${unique_topic}
    Click Button    ${SUBMIT_POST_BUTTON}
    Log    내용 없이 글 내용 작성 및 제출 시도 완료
    ${current_url}=    Get Location
    Should Be Equal As Strings    ${current_url}    http://localhost:4100/editor
    Go To    http://localhost:4100/editor
    Wait Until Page Contains Element    ${POST_TITLE_FIELD}    timeout=10s

작성한 게시글이 목록에 있는지 확인
    Wait Until Page Contains    ${GENERATED_POST_TITLE}    timeout=10s
    Page Should Contain Element    xpath=//h1[text()='${GENERATED_POST_TITLE}']
    Log    프로필에서 작성한 게시글 "${GENERATED_POST_TITLE}" 확인 완료

작성한 게시글 클릭
    Click Link    xpath=//h1[text()='${GENERATED_POST_TITLE}']
    Wait Until Page Contains Element    xpath=//div[@class='container page']//h1
    Log    작성한 게시글 클릭하여 상세 페이지로 이동

해당 게시글의 상세 페이지 URL로 올바르게 이동했는지 확인
    ${current_url}=    Get Location
    Should Contain    ${current_url}    articles/${GENERATED_POST_TITLE}
    Log    게시글 상세 페이지 URL 확인: ${current_url}

글 상세보기 페이지에서 본인 닉네임 클릭 시 내 프로필로 이동하는지 확인
    ${author_profile_link}=    Set Variable    xpath=//div[@class='article-meta']//a[contains(@href, '/@${NAME}')]
    Wait Until Page Contains Element    ${author_profile_link}    timeout=10s
    Click Element    ${author_profile_link}
    Wait Until Page Contains    My Articles    timeout=10s
    Log    글 상세 페이지에서 닉네임 클릭하여 내 프로필로 이동 확인

글 수정 페이지로 이동
    Wait Until Page Contains Element    ${EDIT_ARTICLE_BUTTON}    timeout=10s
    Click Element    ${EDIT_ARTICLE_BUTTON}
    ${current_url}=    Get Location
    Log    Clicked 'Edit Article'. Current URL is: ${current_url}
    Should Contain    ${current_url}    /editor    msg=Should navigate to the editor page for editing.
    Wait Until Page Contains Element    ${POST_TITLE_FIELD}    timeout=20s
    Log    글 수정 페이지로 이동 완료 (Editor page loaded with title field)

제목, 주제, 본문 내용 수정
    Wait Until Page Contains Element    ${POST_TITLE_FIELD}    timeout=20s
    ${modified_title}=    Generate Unique Text    Modified Title 
    ${modified_topic}=    Generate Unique Text    Modified Topic
    ${modified_content}=  Generate Unique Text    This is the modified article content.

    Input Text    ${POST_TITLE_FIELD}    ${modified_title}
    Input Text    ${POST_TOPIC_FIELD}    ${modified_topic}
    Input Text    ${POST_CONTENT_FIELD}    ${modified_content}
    Click Button    ${SUBMIT_POST_BUTTON}
    Log    수정된 글 내용 작성 및 제출 완료
    Set Test Variable    ${MODIFIED_TOPIC}    ${modified_topic}
    Set Test Variable    ${MODIFIED_CONTENT}    ${modified_content}
    Set Test Variable    ${MODIFIED_POST_TITLE}    ${modified_title}

글 상세 페이지에 수정된 제목, 본문 내용이 반영되었는지 확인
    Wait Until Page Contains    ${MODIFIED_POST_TITLE}    timeout=20s
    Page Should Contain    ${MODIFIED_POST_TITLE}
    Page Should Contain    ${MODIFIED_CONTENT}
    Log    수정된 글 내용 상세 페이지에서 확인 완료

글 삭제
    Wait Until Page Contains Element    ${DELETE_ARTICLE_BUTTON}    timeout=10s
    Click Button    ${DELETE_ARTICLE_BUTTON}
    Log    글 삭제 완료

상단 닉네임을 클릭해 내 프로필로 이동
    ${profile_xpath}=    Set Variable    xpath=//a[@class='nav-link' and @href='/@${NAME}' and normalize-space(.)='${NAME}']
    Wait Until Page Contains Element    ${profile_xpath}    timeout=10s
    Click Element    ${profile_xpath}
    Wait Until Page Contains    My Articles    timeout=10s
    Log    상단 닉네임 클릭하여 내 프로필 페이지로 이동 완료

삭제된 게시글이 더 이상 표시되지 않는지 확인
    # ${GENERATED_POST_TITLE} 변수는 이전에 작성했던 글의 제목입니다.
    Page Should Not Contain    ${GENERATED_POST_TITLE}
    Log    프로필에서 삭제된 게시글 "${GENERATED_POST_TITLE}"이 표시되지 않음 확인

글로벌 피드로 이동
    Wait Until Page Contains Element    ${GLOBAL_FEED_LINK}    timeout=10s
    Click Element    ${GLOBAL_FEED_LINK}
    Wait Until Page Contains    Global Feed    timeout=10s

다른 사용자의 게시글 찾아서 클릭
    ${current_username}=    Get Current Username
    Log    Current username for finding other articles: ${current_username}

    Run Keyword If    '${current_username}' == 'None' or '${current_username}' == ''
    ...    Fail    Could not retrieve the current username. Cannot proceed to find other user's article.

    ${found_and_clicked}=    Find And Click Other User Article In Global Feed    ${current_username}
    Log    Result of 'Find And Click Other User Article In Global Feed': ${found_and_clicked}

    Run Keyword If    ${found_and_clicked}
...    Log    Successfully clicked another user's article. Verification of detail page (h1) is now skipped.
...    ELSE
...    Fail    Failed to find and click an article by a different user.

게시글 상세 화면에서 수정/삭제 버튼이 없는지 확인
    Page Should Not Contain Element    ${EDIT_ARTICLE_BUTTON}
    Page Should Not Contain Element    ${DELETE_ARTICLE_BUTTON}
    Log    타인 게시글 상세 화면에서 수정/삭제 버튼 없음 확인